plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id "com.jfrog.artifactory" version "4.24.14"
}

apply plugin: "maven-publish"

bootJar {
    enabled = false
}

jar {
    enabled = true
}

group 'com.youland'
version '1.0-SNAPSHOT'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-webflux'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation group: 'com.google.guava', name: 'guava', version: '30.1-jre'

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
}

test {
    useJUnitPlatform()
}

clean {
    delete "temp", "logs"
}

allprojects {
    apply plugin: "com.jfrog.artifactory"

    repositories {
        maven {
            url "https://youland.jfrog.io/artifactory/teamone-gradle-dev/"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    def ENV = System.getenv()
    def jfrog_contextUrl = ENV['jfrog_contextUrl']
    def jfrog_repo_key = ENV['jfrog_repo_key']
    def jfrog_user = ENV['jfrog_user']
    def jfrog_password = ENV['jfrog_password']

    contextUrl = jfrog_contextUrl
    //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = jfrog_repo_key
            username = jfrog_user
            password = jfrog_password
            maven = true
        }

        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('mavenJava')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            //properties = ['qa.level': 'basic', 'dev.team' : 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
    resolve {
        repository {
            repoKey = jfrog_repo_key
            username = jfrog_user
            password = jfrog_password
            maven = true
        }
    }
}